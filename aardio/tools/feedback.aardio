//反馈  
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio 快速反馈工具";right=774;bottom=615)
winform.add(
btnReport={cls="button";text="提交反馈";left=561;top=86;right=737;bottom=123;color=14120960;dr=1;dt=1;font=LOGFONT(h=-14);z=10};
edit={cls="edit";text="注册:用户名@密码";left=172;top=55;right=317;bottom=81;dl=1;dt=1;edge=1;z=7};
editMessage={cls="edit";left=29;top=151;right=744;bottom=574;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;edge=1;hscroll=1;multiline=1;vscroll=1;z=11};
editPwd={cls="edit";left=408;top=46;right=744;bottom=74;dr=1;dt=1;edge=1;multiline=1;password=1;z=5};
editUser={cls="edit";left=409;top=11;right=744;bottom=39;dr=1;dt=1;edge=1;multiline=1;z=2};
lnkHelp={cls="plus";text="《 提问前必读：解决编程问题的 10 点建议 》";left=86;top=583;right=378;bottom=607;align="left";color=8388608;db=1;dl=1;font=LOGFONT(h=-13);notify=1;textPadding={left=5};z=13};
lnkQuery={cls="plus";text="查询回复消息";left=408;top=85;right=546;bottom=124;align="left";color=7348256;dr=1;dt=1;font=LOGFONT(h=-14);iconStyle={align="left";font=LOGFONT(name='FontAwesome')};iconText='\uF003 ';notify=1;textPadding={left=17};z=14};
plus={cls="plus";left=57;top=24;right=150;bottom=107;bgcolor=16777215;dl=1;dt=1;foreRepeat="scale";foreground="\.res\wx.jpg";repeat="scale";z=6};
plus2={cls="plus";left=29;top=9;right=339;bottom=120;bgcolor=16777215;border={radius=7};dl=1;dr=1;dt=1;z=1};
static={cls="static";text="用户名：";left=316;top=16;right=407;bottom=41;align="right";dl=1;dr=1;dt=1;transparent=1;z=3};
static2={cls="static";text="密码：";left=316;top=55;right=407;bottom=82;align="right";dl=1;dr=1;dt=1;transparent=1;z=4};
static5={cls="static";text="微信公众号发送：";left=172;top=27;right=288;bottom=51;dl=1;dt=1;transparent=1;z=8};
static6={cls="static";text="注册账号";left=172;top=85;right=230;bottom=114;dl=1;dt=1;transparent=1;z=9};
static7={cls="static";text="反馈内容：";left=29;top=128;right=123;bottom=153;dl=1;dt=1;transparent=1;z=12}
)
/*}}*/

import win.clip;
import win.clip.chromiumUrl;
import win.clip.viewer;  

import ide;
import win.version; 
import inet.url; 

import fsys.config;
var config = fsys.config(io.appData("aardio/std/report"));
winform.bindConfig( config.winform,{
	edit = {
		"editUser":"text"
	} 
} );

import ide;
import win.version;
var ideVersion = ide.getConfig("version");

import crypt;
import web.rest.jsonLiteClient;
var http = web.rest.jsonLiteClient("aardio/v" + _AARDIO_VERSION + "(IDE "  + ideVersion + "," + win.version.name + ")");
		
http.beforeStringifyRequestParameters = function(params,codepage){
	if(params.pwd) params.pwd = crypt.sha256(params.pwd);
	return params; 
}
var report = http.api("http://api.aardio.com/api/report/{...}/");

if(!#winform.editMessage.text){
winform.editMessage.text = '- ' + win.version.format() 
	+ '\r\n- aardio v' + ide.getConfig("version")
	+ '\r\n\r\n# 重现问题步骤如下\r\n\r\n- \r\n- \r\n\r\n# 重现问题代码如下\r\n\r\n';	
}

winform.editMessage.setFocus();

winform.editMessage.limit = 204800 - 255; 
	
import win.dlg.message;
winform.btnReport.oncommand = function(id,event){
 
	var user = winform.editUser.text;
	var	pwd = winform.editPwd.text;
	
	if(!#user){
		return winform.editUser.showErrorTip("错误 - 未指定用户名","请指定用户名。使用微信注册用户名以后，才能在微信公众号查询处理结果");
	}
	
	if(!#pwd){
		return winform.editPwd.showErrorTip("错误 - 未指定密码","请指定用户密码。"); 
	}
 
	var msg = winform.editMessage.text;
	
	if(string.len(msg)>204800){
		return winform.msgboxErr("反馈信息总字数不能超过 200 KB !")
	}
	
	if(!#winform.editMessage.text){
		return winform.editMessage.showWarningTip("反馈内容不能为空");
	}
	
	if(!winform.msgAsk("请确认您是否已执行以下操作：
	
1. 已更新  aardio 到最新版。
2. 已点击『 工具 / 扩展库 / 更新 』升级相关扩展库到最新版。
3. 已提供可重现问题步骤与代码，确认可以重现问题，确认未遗漏必要信息。
4. 已检查并确认在其他正常的系统环境可以重现相同问题。
5. 已检索相关文档与范例，已排除低级错误。
6. 已阅读《提问前必读》并执行了相关步骤。
	
每 24 小时只有一次提交机会，请确认已整理好问题。")){
		
		return;
	}
	
	var result = report.post({
		user = winform.editUser.text;
		pwd = pwd;
		url = msg;
	})	
	
	if(result.message){
		winform.msgOk(result.message)
	}
	else {
		winform.msgErr(result.error)
	}
}

winform.editMessage.onChange = function(){ 

	if(#owner.text){
		if(!winform.editMessage.modified){
			return;
		} 	
	}
	else { 
		winform.editMessage.modified = false;	
	} 
}

////参数也可以是返回菜单项数组的函数
import win.clip.bitmap;
winform.editMessage.enablePopMenu(
	function(){
		var canPaste = win.clip.bitmap.test();
		
		return { 
			{ /*---分隔线---*/ };  

				{ "一级标题";  function(id){
					winform.editMessage.selText = '\r\n# 标题\r\n\r\n'
				} };
				{ "二级标题";  function(id){
					winform.editMessage.selText = '\r\n## 标题\r\n\r\n'
				} };  

				{ "无序列表";  function(id){
					winform.editMessage.selText = '
\r\n
* \r\n
* \r\n
* \r\n
\r\n
'
				} };
				{ "有序列表";  function(id){
					winform.editMessage.selText = '
\r\n
1. \r\n
2. \r\n
3. \r\n
\r\n
'
				} };
				{ "引用块";  function(id){
					winform.editMessage.selText = '\r\n>'
				} };
				{ "代码块";  function(id){
					winform.editMessage.selText = '\r\n```\r\n\r\n```'
					winform.editMessage.setsel(winform.editMessage.getsel() - 5)
				} };
		}
	}
)

winform.lnkHelp.oncommand = function(id,event){
	import process;
	process.openUrl("https://mp.weixin.qq.com/s?__biz=MzA3Njc1MDU0OQ==&mid=2650933548&idx=1&sn=1d500b49c6de73f72bf884dd3e01d7cb&chksm=84aa2296b3ddab80cf8393283a884694b6464929c7f890edf26aadaa4738c622229baa4dbbe5&token=629236371&lang=zh_CN#rd")
}
 
winform.lnkQuery.oncommand = function(id,event){
	    
	var user = winform.editUser.text;
	var	pwd = winform.editPwd.text;
	
	if(!#user){
		return winform.editUser.showErrorTip("错误 - 未指定用户名","请指定用户名。使用微信注册用户名以后，才能在微信公众号查询处理结果");
	}
	
	if(!#pwd){
		return winform.editPwd.showErrorTip("错误 - 未指定密码","请指定用户密码。"); 
	}

	if(#user && #pwd){
		var ret = report.query(user=user,pwd=pwd)
		if(!ret){
			winform.msgboxErr("网络错误")
			return;
		}
		 
		if(ret.message){ 
			
			var frmWeb = win.form(text="反馈回复",parent = winform)
			
			import web.form.simpleMarkdown;
			var wb = web.form.simpleMarkdown(frmWeb);
			wb.noScriptErr = true;
			wb.write(ret.message);
			
			import win.clip
			win.clip.write( wb.body.innerHTML)
			
			wb.BeforeNavigate2=function( pDisp, url,flags,target,postData,headers,cancel ) { 
				import ide;
				
				if(string.startWith(url,"doc://")){ 
					ide.openDocument(url); 
				}
				elseif(string.startWith(url,"\\~\",true) 
					&& (string.endWith(url,".aardio",true) || string.endWith(url,".aproj",true) ) 
					){
						url = string.right(url,-3);
						ide.openDocument(url); 
				}
				else {
					..raw.execute(url);
				}
 				
				return url, flags, target, postData, headers,true;
			}
			
			frmWeb.doModal();
			
			winform.lnkQuery.text = "您的反馈已收到回复，点这里查看！"
		}
		elseif(ret.error) {
			winform.msgboxErr(ret.error)
		} 
		else {
			if(ret.status===null){
				winform.msgbox("你在近期未提交任何反馈");
			}
			else {
				winform.msgbox("您的反馈正在处理中，请耐心等待回复。")
			}
			
		}
		
	} 
}

winform.lnkHelp.skin({
	color={
		active=0xFF00FF00;
		default=0xE5000080;
		disabled=0xFF6D6D6D;
		hover=0xFFFF0000		
	}
})

winform.lnkQuery.skin({
	color={
		active=0xFF00FF00;
		default=0xE5000080;
		disabled=0xFF6D6D6D;
		hover=0xFFFF0000		
	}
})

winform.show();
win.loopMessage();