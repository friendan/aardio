//jsonClient 客户端
import web.json;
import web.rest.client;
namespace web.rest; 

class jsonClient{
	ctor( ... ){
		this = ..web.rest.client( ... ); 
		this.stringifyRequestParameters = stringifyRequestParameters;
		this.parseResponseResult = function(s,streamChunk){
			if( s="" ) return s;
			var t = this.lastResponseContentType; 

			if(t){  
				if( this._http.ndjsonStream && !streamChunk  ) { 
					return ..web.json.ndParse(s,true);
				} 
				
				if( ..string.match( t,"[\w\/]*<@@json@>![\W]"  ) ) return ..web.json.parse(s,true); 
				
				if(t=="application/x-www-form-urlencoded"){
					var r = ..inet.url.splitParameters(s);
					for(k,v in r){
						if(v[1]=='{'# || v[1]=='['#){
							r[k] = ..web.json.tryParse(v,true) : v;
						}
					} 
					return r;
				}
			}
			
			s = ..string.trim(s);
			if( s[1] == '{'# || s[1] == '['# ){
				return  ..web.json.tryParse(s,true);
			}
			
			if( s[1]  == '<'# ) return s; 
			
			if(this.strictParsing) return s;
			return  ..web.json.tryParse(s,true) : s;
		}
		
		this.contentType = "application/json";  
		this.defaultContentType = "application/json";
		this.acceptType = "application/json,text/json,*/*";
	}; 
	lastResponse = function(){
		var s = this.lastResponseData;
		if( s ){
			s = ..string.trim(s);
			 
		 	if( (..string.match( this._http.responseContentType,"[\w\/]*<@@json@>![\W]"  ) 
		 		|| s[1] == '{'# || s[1] == '['#) && (!this._http.lastReadErrCode ) ){
		 			var jObject = ..web.json.parse(s,true);
		 			if(jObject) s = ..web.json.stringify(jObject,true);
		 	}
		 	if( ..console && _STUDIO_INVOKED ) ..console.log(s) else ..io.print(s);
		 	return s;
		}
	};
}

namespace jsonClient {

	var stringify = ..web.json.stringify;
	stringifyRequestParameters = function(param/*,codepage*/){
		param = stringify(param,false,true); //转换为UNICODE
		return param;
	} 
}

/**intellisense()
web.rest.jsonClient = REST 客户端。\n请求数据，返回数据都使用 JSON 格式
web.rest.jsonClient( = 创建 REST 客户端。\n请求数据，返回数据都使用 JSON 格式
web.rest.jsonClient(.("UserAgent","代理服务器","绕过代理的主机",连接选项) = 创建 REST 客户端，所有参数可选。\nUserAgent 用于自定义 User-Agent 请求标头，用于服务器识别请求程序特征。\nHTTP 代理服务器请指定为 "代理服务器地址:端口" 格式，\n\nSOCKS 代理服务器请指定为 "socks=代理服务器地址:端口" 格式 \n└──  [代理格式说明](doc://library-guide/std/inet/proxy.md)\n\n"绕过代理的主机" 用法参考 inet.setProxy 源码的示例，一般不需要指定。\n连接选项可用一个数值参数指定打开会话的选项，一般不需要指定。
web.rest.jsonClient() = !web_restClient.
end intellisense**/