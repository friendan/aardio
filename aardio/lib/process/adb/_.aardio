//adb 安卓设备管理
import wsock;
import wsock.tcp.client;
import process.popen;
import process.job.limitKill;
import string.cmdline;
import process.cmdline;
import inet.http;
import win.path;

namespace process;

class adb{
	ctor( param,... ){
		var args = ..string.cmdline.arguments(param,... )
		if( defaultArguments ) args = ..table.append({},defaultArguments,args);

		this = ..process.popen(self.path,args,{workDir=workDir});
		this.assignToJobObject(..process.job.limitKill);
		this.codepage = 65001;
		
		if( !this ) return;
		
		if( param == "shell" && !... ){
			this.expect("\N+([\$#]) $",3000);
			this.__close__ = this.close;
			this.close = function(){
				if( this.stdIn ){ 
					this.print('exit');
					sleep(10);
					this.__close__();
				}
			}
		} 
	};
	
}

namespace adb{ 
	
	workDir = "/";
	
	get = function(...){ 
		var adb,err = ..process.adb( ...);
		if(!adb) return null,err;
		
		var out = adb.read(-1); 
		var err = adb.readErr(-1);
		adb.close();

		if(#err) return null,err;
		
		if(#out){
			out = ..string.trim(out);
			err = ..string.match(out,"^adb\:?\s+error\:(.+)") 
			if(err) return null,err;	 
		} 
		
		return out;  
	}
	
	shell = function(...){
		return ..process.adb("shell",...);
	}
	
	shellGet = function(...){
		var adb,err = ..process.adb.shell(...);
		if(!adb) return null,err;
		
		var out = adb.read(-1); 
		var err = adb.readErr(-1);
		adb.close();
		 
		if(#out) return ..string.trim(out);
		if(#err)return null,err
		return out;
	}
	
	startCall = function(phone){
		return shellGet("am start -a android.intent.action.CALL -d tel:"+phone); 
	}
	
	startUrl = function(url){
		return shellGet("am start -a android.intent.action.VIEW -d '"+url+"'"); 
	}
	
	screenCap = function(path){
		var filename = "adb_"+..io._exefile + "_screen.png";
		var out,err = shellGet("screencap -p $EXTERNAL_STORAGE/"+filename+" && echo $EXTERNAL_STORAGE/"+filename); 
	 	if(#err) return null,err; 
	 	
		if(out) {
			out,err = pullGet(out,..io.localpath(path)||path); 
		}
		shellGet("rm $EXTERNAL_STORAGE/"+filename);  
		
		if(#err) return null,err;
		return true;
	}
	 
	push = function(local,remote,...){
		if(!remote) remote = "/mnt/sdcard/";
		//大文件无法显示进度,adb 就这样，没其他办法
		var adb = ..process.adb("push",..io.localpath(local)||local,remote,... );
		adb.logger = logger;
		adb.logResponse();
		adb.close(); 
	} 
	
	pull = function(remote,local,...){  
		//大文件无法显示进度,adb 就这样，没其他办法
		var adb = ..process.adb("pull",remote,..io.localpath(local)||local,... );
		adb.logger = logger;
		adb.logResponse();
		adb.close(); 
	}
	
	pushGet = function(local,remote,...){
		if(!remote) remote = "/mnt/sdcard/";
		return get("push",..io.localpath(local)||local,remote,... ); 
	}
	
	pullGet = function(remote,local,...){ 
		return get("pull",remote,..io.localpath(local)||local,... );
	}
	
	install = function(local,...){ 
		//大文件无法显示进度,adb 就这样，没其他办法
		var adb = ..process.adb("install",..io.fullpath(local),... );
		adb.logger = logger;
		adb.logResponse();
		adb.close(); 
	}
	
	userAgent = "Mozilla/5.0 (Linux; U; Android 12; ) AppleWebKit/537.36 (KHTML, like Gecko) Version/4.0 Chrome/100.0.4896.127 Mobile Safari/537.36";
	
	installUrl = function(url,...){
		if(..io.exist(url)){
			return install(url);
		}
		 
		var path = ..thread.invokeAndWait(
			function(url,userAgent){
				import inet.http;
				var http = inet.http(userAgent);
				
				var apk = http.get(url);
				if(apk && ..string.left(apk,4) =='\x50\x4b\x03\x04'){ 
					var path = ..io.tmpname("adb",".apk");
					..string.save(path,apk);
					return path;
				}
				elseif(apk) {  
					var apkUrl = ..string.match(apk,"https?\://[^\s\""']+arm64[^\s\""']+\.apk") || ..string.match(apk,"https?\://[^\s\""']+.apk");
					if(apkUrl){
						var apk = http.get(apkUrl);
						if(apk && ..string.left(apk,4) =='\x50\x4b\x03\x04'){ 
							var path = ..io.tmpname("adb",".apk");
							..string.save(path,apk);
							return path;
						}	
					}
				}
			},url,userAgent
		)

		if( path ){
			install(path,...);
			..io.remove(path); 
		}
	}
	
	uninstall = function(app,...){ 
		var adb = ..process.adb("uninstall",app,... ) 
		adb.logger = logger;
		adb.logResponse(); 
		adb.close(); 
	} 
	
	getSerialNo = function(){ 
		return get("get-serialno") 
	}
	
	setSerialNo = function(device){
		..string.setenv("ANDROID_SERIAL",device);
		return device;
	}
	 
	getState = function(serialNo){ 
		if(serialNo) return get("-s",serialNo,"get-state");
		return get("get-state")  
	}
	
	version = function(android){
		if(android) return getProp("ro.build.version.release");
		return get("--version")   
	}
	
	findDevice = function(usb){
		var devices = getDevices();
		if(#devices){
			if(#devices==1){
				return setSerialNo(devices[1].serialNo); 
			}
			 
			if(type.isString(usb)){
				for(i,dev in devices){
					if(dev.serialNo == usb){
						return setSerialNo(dev.serialNo); 
					}
				} 	
			}
			else {
				for(i,dev in devices){
					if(!usb || !..string.indexOf(dev.serialNo,":")){
						return setSerialNo(dev.serialNo);
					}
				}  	
			}  
			
			return setSerialNo(devices[1].serialNo); 
		}
	}
	
	getDevices = function(offline){ 
		var adb = ..process.adb( "devices","-l" )
		var out  = adb.read(-1);
		adb.close();
		if(out) {
			var ret = {}
			for dev in ..string.lines(..string.trim(out),,"\s+",3){
				if(#dev==3 && (dev[2]=="device" || (dev[2]=="offline" && offline)) ){
					var info = {};
					for k,v in ..string.gmatch(dev[3],"(\S+)\:(\S+)") { 
		    			info[k] = v;
					}
					..table.push(ret,{serialNo=dev[1];state=dev[2];info=info});
				} 
			}
			
			return ret;
		}
	} 
	
	waitForDevice = function(){ 
		return get( "wait-for-device" );
	}
	
	startServer = function(){
		port = findServerPort();
		if(port){
			..string.setenv("ANDROID_ADB_SERVER_PORT",port);
			return true; 
		}
		else {
			port = "5037";
			if(..wsock.tcp.client.test("127.0.0.1",5037)){
				port = tostring(..wsock.getFreePort());
			} 
			
			..string.setenv("ANDROID_ADB_SERVER_PORT",port);
		} 

		var adb,err = ..process.popen( self.path,"start-server"); 
		if(!adb) return null,err;
		
		var out,err = adb.expect("daemon started successfully",3000)
		adb.close();
		
		if( out ) return true,out;
		if( err ) return false,err;
		return true;
	}
	
	killServer = function(){ 
		var adb = ..process.adb( "kill-server" )
		adb.waitOne();
		adb.close(); 
	}
	
	findServerPort = function(){ 
		for processEntry in ..process.each( "adb\.exe" ) { 
			var cmdline = ..process.cmdline(processEntry.th32ProcessID);
			if(cmdline){
				var argv = ..string.cmdline.argv(cmdline);
				if(argv.L && argv["reply-fd"] && ..table.indexOf(argv,"fork-server") ){
					var port = ..string.match(argv.L,"tcp\:(\d+)")
					if(port){
						return port;		
					} 
				}  
			} 
		} 
	} 
	
	pair = function(addr,code){
		var prcs = ..process.adb("pair",addr);
		if( prcs.expect("Enter\s+pairing\s+code\:") ){
			prcs.print(code)
		}

		var r,e = prcs.expect("Successfully\s+paired\s+to\s+.+\[(.+)\]");
		prcs.close();
		
		return r,e;
	}
	
	disconnect = function(){
		return get("disconnect");
	}
	
	lastAddress = function(addr){
		if(addr){
			..string.save("aardio/process/adb/$$.ADDR",addr);
			return addr;
		}
		
		addr = ..string.load( ..io.appData("aardio/process/adb/$$.ADDR") );
		if(#addr){
			return addr;
		}
	} 
	 
	connect = function(addr,changeSerialNo){
		if( !addr ){
			addr = lastAddress();
			if(!#addr) return null,"参数 @1 必须指定连接地址";
		}
		
		var r,e = ..process.adb("connect",addr).readAll(); 
		if(r){
			var r2 = ..string.match(r,"connected\s+to\s+(\S+)")
			if(r2) { 
				if(!..string.indexOf(addr,":")) addr = addr + ":5555";
				if((changeSerialNo===null) || changeSerialNo )setSerialNo(addr);
				return r2;
			}
			
			return null,r;
		}
		
		return r,e; 
	}
	
	isConnected = function(){
		return "device" == getState();
	}
	
	tcpip = function(port,save){
		var ip = ip();
		
		if(port===false)return get("usb");
		if(!port) port = 5555;
		
		var out,err = get("tcpip",port);
		if(out){
			var addr = ..string.match(out,"connected\s+to\s+(\S+)");
			if(!addr && ip) addr = ip + ":" + port;
			
			if(save){
				lastAddress(addr)
			}
			return addr; 
		}
		return out,err;
	}
	
	ip = function(){
		var out,err = shellGet("ip addr show wlan0");
		if(!out)return null,err;
		
		return ..string.match(out,"inet\s*(\d+\.\d+\.\d+\.\d+)/\d+");
	}
 
	getScreen = function(){
		var out,err = shellGet("wm size");
		if(!out)return null,err;
		 
		var cx,cy = ..string.match(out,"Physical\s*size\D+(\d+)x(\d+)");
		return tonumber(cx),tonumber(cy);
	}
	
	getMaxPos = function(){
		var out,err = shellGet("getevent -il");
		if(!out)return null,err;
		 
		var x = ..string.match(out,"ABS_MT_POSITION_X\s*\:\N+?max\s+(\d+)");
		var y = ..string.match(out,"ABS_MT_POSITION_Y\s*\:\N+?max\s+(\d+)");
		return tonumber(x),tonumber(y);
	} 
	
	/*
	https://source.android.com/docs/core/interaction/input/getevent?hl=zh-cn
	-t  显示每个事件的时间戳
	-l 以更具可读性的方式显示事件，显示事件名称和值。
	-n 不显示设备信息，只显示事件数据。
	-c [COUNT] 读取指定数量的事件后停止。
	-q 安静模式，只显示事件数据，不显示其他信息。
	一个短横线后可写多个短参数
	*/
	getEvent = function(...){ 
		return shell(`getevent`,...);    
	} 
	
	input = λ(...) shellGet("input",...); 
	tap = λ(x,y) shellGet("input","tap",x,y); 
	swipe = λ(x1,y1,x2,y2,duration) shellGet("input","swipe",x1,y1,x2,y2,duration);
	
	sendString = λ(text) shellGet("input","text",text); 
	key = λ(code,longPress) shellGet("input","keyevent",longPress?"--longpress":"",code); 
	sendevent = λ(...) shellGet("sendevent",...); 
	
	/*intellisense(process.adb)*/ 
	keyUnknown = λ(l) key(0,l);
	keySoftLeft = λ(l) key(1,l);
	keySoftRight = λ(l) key(2,l);
	keyHome = λ(l) key(3,l);
	keyBack = λ(l) key(4,l);
	keyCall = λ(l) key(5,l);
	keyEndcall = λ(l) key(6,l);
	key0 = λ(l) key(7,l);
	key1 = λ(l) key(8,l);
	key2 = λ(l) key(9,l);
	key3 = λ(l) key(10,l);
	key4 = λ(l) key(11,l);
	key5 = λ(l) key(12,l);
	key6 = λ(l) key(13,l);
	key7 = λ(l) key(14,l);
	key8 = λ(l) key(15,l);
	key9 = λ(l) key(16,l);
	keyStar = λ(l) key(17,l);
	keyPound = λ(l) key(18,l);
	keyDpadUp = λ(l) key(19,l);
	keyDpadDown = λ(l) key(20,l);
	keyDpadLeft = λ(l) key(21,l);
	keyDpadRight = λ(l) key(22,l);
	keyDpadCenter = λ(l) key(23,l);
	keyVolumeUp = λ(l) key(24,l);
	keyVolumeDown = λ(l) key(25,l);
	keyPower = λ(l) key(26,l);
	keyCamera = λ(l) key(27,l);
	keyClear = λ(l) key(28,l);
	keyA = λ(l) key(29,l);
	keyB = λ(l) key(30,l);
	keyC = λ(l) key(31,l);
	keyD = λ(l) key(32,l);
	keyE = λ(l) key(33,l);
	keyF = λ(l) key(34,l);
	keyG = λ(l) key(35,l);
	keyH = λ(l) key(36,l);
	keyI = λ(l) key(37,l);
	keyJ = λ(l) key(38,l);
	keyK = λ(l) key(39,l);
	keyL = λ(l) key(40,l);
	keyM = λ(l) key(41,l);
	keyN = λ(l) key(42,l);
	keyO = λ(l) key(43,l);
	keyP = λ(l) key(44,l);
	keyQ = λ(l) key(45,l);
	keyR = λ(l) key(46,l);
	keyS = λ(l) key(47,l);
	keyT = λ(l) key(48,l);
	keyU = λ(l) key(49,l);
	keyV = λ(l) key(50,l);
	keyW = λ(l) key(51,l);
	keyX = λ(l) key(52,l);
	keyY = λ(l) key(53,l);
	keyZ = λ(l) key(54,l);
	keyComma = λ(l) key(55,l);
	keyPeriod = λ(l) key(56,l);
	keyAltLeft = λ(l) key(57,l);
	keyAltRight = λ(l) key(58,l);
	keyShiftLeft = λ(l) key(59,l);
	keyShiftRight = λ(l) key(60,l);
	keyTab = λ(l) key(61,l);
	keySpace = λ(l) key(62,l);
	keySym = λ(l) key(63,l);
	keyExplorer = λ(l) key(64,l);
	keyEnvelope = λ(l) key(65,l);
	keyEnter = λ(l) key(66,l);
	keyDel = λ(l) key(67,l);
	keyGrave = λ(l) key(68,l);
	keyMinus = λ(l) key(69,l);
	keyEquals = λ(l) key(70,l);
	keyLeftBracket = λ(l) key(71,l);
	keyRightBracket = λ(l) key(72,l);
	keyBackslash = λ(l) key(73,l);
	keySemicolon = λ(l) key(74,l);
	keyApostrophe = λ(l) key(75,l);
	keySlash = λ(l) key(76,l);
	keyAt = λ(l) key(77,l);
	keyNum = λ(l) key(78,l);
	keyHeadsethook = λ(l) key(79,l);
	keyFocus = λ(l) key(80,l);
	keyPlus = λ(l) key(81,l);
	keyMenu = λ(l) key(82,l);
	keyNotification = λ(l) key(83,l);
	keySearch = λ(l) key(84,l);
	keyMediaPlayPause = λ(l) key(85,l);
	keyMediaStop = λ(l) key(86,l);
	keyMediaNext = λ(l) key(87,l);
	keyMediaPrevious = λ(l) key(88,l);
	keyMediaRewind = λ(l) key(89,l);
	keyMediaFastForward = λ(l) key(90,l);
	keyMute = λ(l) key(91,l);
	keyPageUp = λ(l) key(92,l);
	keyPageDown = λ(l) key(93,l);
	keyPictsymbols = λ(l) key(94,l);
	keySwitchCharset = λ(l) key(95,l);
	keyButtonA = λ(l) key(96,l);
	keyButtonB = λ(l) key(97,l);
	keyButtonC = λ(l) key(98,l);
	keyButtonX = λ(l) key(99,l);
	keyButtonY = λ(l) key(100,l);
	keyButtonZ = λ(l) key(101,l);
	keyButtonL1 = λ(l) key(102,l);
	keyButtonR1 = λ(l) key(103,l);
	keyButtonL2 = λ(l) key(104,l);
	keyButtonR2 = λ(l) key(105,l);
	keyButtonThumbl = λ(l) key(106,l);
	keyButtonThumbr = λ(l) key(107,l);
	keyButtonStart = λ(l) key(108,l);
	keyButtonSelect = λ(l) key(109,l);
	keyButtonMode = λ(l) key(110,l);
	keyEscape = λ(l) key(111,l);
	keyForwardDel = λ(l) key(112,l);
	keyCtrlLeft = λ(l) key(113,l);
	keyCtrlRight = λ(l) key(114,l);
	keyCapsLock = λ(l) key(115,l);
	keyScrollLock = λ(l) key(116,l);
	keyMetaLeft = λ(l) key(117,l);
	keyMetaRight = λ(l) key(118,l);
	keyFunction = λ(l) key(119,l);
	keySysrq = λ(l) key(120,l);
	keyBreak = λ(l) key(121,l);
	keyMoveHome = λ(l) key(122,l);
	keyMoveEnd = λ(l) key(123,l);
	keyInsert = λ(l) key(124,l);
	keyForward = λ(l) key(125,l);
	keyMediaPlay = λ(l) key(126,l);
	keyMediaPause = λ(l) key(127,l);
	keyMediaClose = λ(l) key(128,l);
	keyMediaEject = λ(l) key(129,l);
	keyMediaRecord = λ(l) key(130,l);
	keyF1 = λ(l) key(131,l);
	keyF2 = λ(l) key(132,l);
	keyF3 = λ(l) key(133,l);
	keyF4 = λ(l) key(134,l);
	keyF5 = λ(l) key(135,l);
	keyF6 = λ(l) key(136,l);
	keyF7 = λ(l) key(137,l);
	keyF8 = λ(l) key(138,l);
	keyF9 = λ(l) key(139,l);
	keyF10 = λ(l) key(140,l);
	keyF11 = λ(l) key(141,l);
	keyF12 = λ(l) key(142,l);
	keyNumLock = λ(l) key(143,l);
	keyNumpad0 = λ(l) key(144,l);
	keyNumpad1 = λ(l) key(145,l);
	keyNumpad2 = λ(l) key(146,l);
	keyNumpad3 = λ(l) key(147,l);
	keyNumpad4 = λ(l) key(148,l);
	keyNumpad5 = λ(l) key(149,l);
	keyNumpad6 = λ(l) key(150,l);
	keyNumpad7 = λ(l) key(151,l);
	keyNumpad8 = λ(l) key(152,l);
	keyNumpad9 = λ(l) key(153,l);
	keyNumpadDivide = λ(l) key(154,l);
	keyNumpadMultiply = λ(l) key(155,l);
	keyNumpadSubtract = λ(l) key(156,l);
	keyNumpadAdd = λ(l) key(157,l);
	keyNumpadDot = λ(l) key(158,l);
	keyNumpadComma = λ(l) key(159,l);
	keyNumpadEnter = λ(l) key(160,l);
	keyNumpadEquals = λ(l) key(161,l);
	keyNumpadLeftParen = λ(l) key(162,l);
	keyNumpadRightParen = λ(l) key(163,l);
	keyVolumeMute = λ(l) key(164,l);
	keyInfo = λ(l) key(165,l);
	keyChannelUp = λ(l) key(166,l);
	keyChannelDown = λ(l) key(167,l);
	keyZoomIn = λ(l) key(168,l);
	keyZoomOut = λ(l) key(169,l);
	keyTv = λ(l) key(170,l);
	keyWindow = λ(l) key(171,l);
	keyGuide = λ(l) key(172,l);
	keyDvr = λ(l) key(173,l);
	keyBookmark = λ(l) key(174,l);
	keyCaptions = λ(l) key(175,l);
	keySettings = λ(l) key(176,l);
	keyTvPower = λ(l) key(177,l);
	keyTvInput = λ(l) key(178,l);
	keyStbPower = λ(l) key(179,l);
	keyStbInput = λ(l) key(180,l);
	keyAvrPower = λ(l) key(181,l);
	keyAvrInput = λ(l) key(182,l);
	keyProgRed = λ(l) key(183,l);
	keyProgGreen = λ(l) key(184,l);
	keyProgYellow = λ(l) key(185,l);
	keyProgBlue = λ(l) key(186,l);
	keyAppSwitch = λ(l) key(187,l);
	keyButton1 = λ(l) key(188,l);
	keyButton2 = λ(l) key(189,l);
	keyButton3 = λ(l) key(190,l);
	keyButton4 = λ(l) key(191,l);
	keyButton5 = λ(l) key(192,l);
	keyButton6 = λ(l) key(193,l);
	keyButton7 = λ(l) key(194,l);
	keyButton8 = λ(l) key(195,l);
	keyButton9 = λ(l) key(196,l);
	keyButton10 = λ(l) key(197,l);
	keyButton11 = λ(l) key(198,l);
	keyButton12 = λ(l) key(199,l);
	keyButton13 = λ(l) key(200,l);
	keyButton14 = λ(l) key(201,l);
	keyButton15 = λ(l) key(202,l);
	keyButton16 = λ(l) key(203,l);
	keyLanguageSwitch = λ(l) key(204,l);
	keyMannerMode = λ(l) key(205,l);
	key3dMode = λ(l) key(206,l);
	keyContacts = λ(l) key(207,l);
	keyCalendar = λ(l) key(208,l);
	keyMusic = λ(l) key(209,l);
	keyCalculator = λ(l) key(210,l);
	keyZenkakuHankaku = λ(l) key(211,l);
	keyEisu = λ(l) key(212,l);
	keyMuhenkan = λ(l) key(213,l);
	keyHenkan = λ(l) key(214,l);
	keyKatakanaHiragana = λ(l) key(215,l);
	keyYen = λ(l) key(216,l);
	keyRo = λ(l) key(217,l);
	keyKana = λ(l) key(218,l);
	keyAssist = λ(l) key(219,l);
	keyBrightnessDown = λ(l) key(220,l);
	keyBrightnessUp = λ(l) key(221,l);
	keyMediaAudioTrack = λ(l) key(222,l);
	keySleep = λ(l) key(223,l);
	keyWakeup = λ(l) key(224,l);
	keyPairing = λ(l) key(225,l);
	keyMediaTopMenu = λ(l) key(226,l);
	key11 = λ(l) key(227,l);
	key12 = λ(l) key(228,l);
	keyLastChannel = λ(l) key(229,l);
	keyTvDataService = λ(l) key(230,l);
	keyVoiceAssist = λ(l) key(231,l);
	keyTvRadioService = λ(l) key(232,l);
	keyTvTeletext = λ(l) key(233,l);
	keyTvNumberEntry = λ(l) key(234,l);
	keyTvTerrestrialAnalog = λ(l) key(235,l);
	keyTvTerrestrialDigital = λ(l) key(236,l);
	keyTvSatellite = λ(l) key(237,l);
	keyTvSatelliteBs = λ(l) key(238,l);
	keyTvSatelliteCs = λ(l) key(239,l);
	keyTvSatelliteService = λ(l) key(240,l);
	keyTvNetwork = λ(l) key(241,l);
	keyTvAntennaCable = λ(l) key(242,l);
	keyTvInputHdmi1 = λ(l) key(243,l);
	keyTvInputHdmi2 = λ(l) key(244,l);
	keyTvInputHdmi3 = λ(l) key(245,l);
	keyTvInputHdmi4 = λ(l) key(246,l);
	keyTvInputComposite1 = λ(l) key(247,l);
	keyTvInputComposite2 = λ(l) key(248,l);
	keyTvInputComponent1 = λ(l) key(249,l);
	keyTvInputComponent2 = λ(l) key(250,l);
	keyTvInputVga1 = λ(l) key(251,l);
	keyTvAudioDescription = λ(l) key(252,l);
	keyTvAudioDescriptionMixUp = λ(l) key(253,l);
	keyTvAudioDescriptionMixDown = λ(l) key(254,l);
	keyTvZoomMode = λ(l) key(255,l);
	keyTvContentsMenu = λ(l) key(256,l);
	keyTvMediaContextMenu = λ(l) key(257,l);
	keyTvTimerProgramming = λ(l) key(258,l);
	keyHelp = λ(l) key(259,l);
	keyNavigatePrevious = λ(l) key(260,l);
	keyNavigateNext = λ(l) key(261,l);
	keyNavigateIn = λ(l) key(262,l);
	keyNavigateOut = λ(l) key(263,l);
	keyStemPrimary = λ(l) key(264,l);
	keyStem1 = λ(l) key(265,l);
	keyStem2 = λ(l) key(266,l);
	keyStem3 = λ(l) key(267,l);
	keyDpadUpLeft = λ(l) key(268,l);
	keyDpadDownLeft = λ(l) key(269,l);
	keyDpadUpRight = λ(l) key(270,l);
	keyDpadDownRight = λ(l) key(271,l);
	keyMediaSkipForward = λ(l) key(272,l);
	keyMediaSkipBackward = λ(l) key(273,l);
	keyMediaStepForward = λ(l) key(274,l);
	keyMediaStepBackward = λ(l) key(275,l);
	keySoftSleep = λ(l) key(276,l);
	keyCut = λ(l) key(277,l);
	keyCopy = λ(l) key(278,l);
	keyPaste = λ(l) key(279,l);
	keySystemNavigationUp = λ(l) key(280,l);
	keySystemNavigationDown = λ(l) key(281,l);
	keySystemNavigationLeft = λ(l) key(282,l);
	keySystemNavigationRight = λ(l) key(283,l);
	keyAllApps = λ(l) key(284,l);
	keyRefresh = λ(l) key(285,l);
	/*end intellisense*/
	
	getProp = function(name){
		return shellGet("getprop",name)
	}
	
	listPackages = function(p,...){
		var o,e;
		if(...){
			o,e = shellGet("pm","list","packages",p,...)
		}
		else {
			o,e = shellGet("pm list packages " + (p:""))
		}
		
		if(!e && o){
			return ..string.splitEx(o);
		}
		
		return null,o;
	} 
	
	path = ..io.appData("aardio/std/adb/20240409/adb.exe",$"~\lib\process\adb\.res\adb.exe");
	..io.appData("aardio/std/adb/20240409/AdbWinApi.dll",$"~\lib\process\adb\.res\AdbWinApi.dll");
	..io.appData("aardio/std/adb/20240409/AdbWinUsbApi.dll",$"~\lib\process\adb\.res\AdbWinUsbApi.dll");
	
	..string.setenv("ADB",path)
	..win.path.add(..io.appData("aardio/std/adb/20240409/"),,true); 
	
	startServer();
}

/**details(入门必读)
adb 命令速查与参考文档： 
https://quickref.me/zh-CN/docs/adb.html
https://adbshell.com/
http://developer.android.com/tools/help/adb.html

手机启用 adb 调试步骤
https://developer.android.com/tools/adb?hl=zh-cn#Enabling
要点：点按手机版本号选项七次才会显示开发者选项

process.adb.startServer 启动 adb 服务端，这个函数实际上会自动调用一次，
adb 需要一个常驻服务端（启动一个 adb.exe 进程），也只能有一个服务进程，多个服务进程会工作不正常。
这个扩展库会自动查找之前启动的 adb 服务端，如果找到就直接重用。
进程退出时不会退出常驻服务端（一般无此必要 ）。

每次发送 adb 指令时也会启动一个 adb 客户端，这也是一个 adb.exe 进程。
所有 adb 客户端在执行完成后，或当前进程退出后将会自动关闭（由 process.job.limitKill 实现）。

只有首次连接需要配对。
在手机无线调试页面可以看到动态端口，首次用该端口连接。
然后执行下面的命令设置固定端口，并保存连接地址到 APPDATA。
process.adb.tcpip("5555",true)

也可以用 USB 线连接手机直接执行上面的 tcpip 函数。

以后就可以无参数直接调用 process.adb.connect() 连接手机了。 
end details**/

/**intellisense()
process.adb = 使用管道调用 adb。\n调用 process.adb.startServer 函数启动 adb 服务端，\n该函数会重用已启动的 adb 服务端而不会重复启动。\n该函数启动的 adb 服务端为驻留进程，不会随程序退出，\n\n发送其他 adb 指定则启动 adb 客户端。\n此扩展库启动的所有 adb 客户端在执行结束或程序退出时自动关闭。
process.adb( = 执行 adb 命令，返回 process.popen 进程管道对象。
process.adb(.(->->->) = 执行 adb 命令，返回 process.popen 进程管道对象。\n可传入一个或多个启动命令行参数（字符串）。\n传入单个字符串参数时，可用空格分开多个命令行参数。\n如果有多个命令行参数，则不在双引号内、且包含空白或需要转义的参数时，\n转义处理该参数后并在首尾添加双引号。\n也可用一个数组传入多个参数
process.adb.get(.(->->->) = 执行 adb 命令。\n成功返回进程输出，出错返回 null，错误信息 。\n\n可传入一个或多个启动命令行参数（字符串）。\n传入单个字符串参数时，可用空格分开多个命令行参数。\n如果有多个命令行参数，则不在双引号内、且包含空白或需要转义的参数时，\n转义处理该参数后并在首尾添加双引号。\n也可用一个数组传入多个参数
process.adb.shellGet(.(->->->) = 执行 adb shell 命令。\n成功返回进程输出，出错返回 null，错误信息 。\n\n可传入一个或多个启动命令行参数（字符串）。\n传入单个字符串参数时，可用空格分开多个命令行参数。\n如果有多个命令行参数，则不在双引号内、且包含空白或需要转义的参数时，\n转义处理该参数后并在首尾添加双引号。\n也可用一个数组传入多个参数
process.adb.shell(__) = 启动 adb 客户端并返回  process.popen 对象,\n指定参数则直接执行参数指定的命令后关闭进程。\n\n不指定参数则进入 shell 命令交互模式,\n交互模式下可使用 expect 函数检测并等待进程输出。\n\n更多用法请参考 process.popen 库函数文档
process.adb() = !process_popen.
process.adb.shell() = !process_popen. 
process.adb.workDir = 工作目录，默认为 "/"（应用程序根目录）;
process.adb.path = adb.exe 路径
end intellisense**/

/**intellisense(process.adb)
version() = 返回 ADB 版本信息。
version(true) = 返回手机设备 Android 版本。
defaultArguments = 可用一个数组指定添加到所有 ADB 命令的默认参数，默认为 null。\n默认参数会添加到其他参数前面。\n\n如果默认参数添加了 {"-s","设备序号"} 以发送到特定设备，\n则覆盖 setSerialNo 函数指定的目标设备
logger = 设置 push,pull,install,uninstall 等命令的默认回显对象。\n此对象用于 process.popen 的 logger 对象，\n可以设为函数或文本框控件，如果引入了 console 库则会默认回显到控制台。
push( = 上传文件到手机\n此函数调用 process.adb.logger 显示进程输出。\n如果要直接返回结果，请改用 pushGet 函数
push(.("本地路径","/mnt/sdcard/",->->->) = 上传文件到手机。\n注意本地当前目录为 process.adb.workDir 指定的目录，默认为"/"。\n\n如果进程输出 Permission Denied 无权限，\n建议请打开手机 USB 调试模式,尝试 root 修改目录权限。\n可用 process.adb.logger 预先设置进程回显对象
pull( = 自手机下载文件。\n此函数调用 process.adb.logger 显示进程输出。\n如果要直接返回结果，请改用 pullGet 函数
pull(.("/mnt/sdcard/my->txt","本地路径",->->->) = 自手机下载文件。\n注意本地当前目录为 process.adb.workDir 指定的目录，默认为"/"。\n\n如果进程输出 Permission Denied 无权限，\n建议请打开手机 USB 调试模式,尝试 root 修改目录权限。\n可用 process.adb.logger 预先设置进程回显对象
pushGet( = 上传文件到手机。\n成功返回 true ，失败返回 null, 错误信息。\n此函数不会调用 process.adb.logger 输出
pushGet(.("本地路径","/mnt/sdcard/",->->->) = 上传文件到手机。\n注意本地当前目录为 process.adb.workDir 指定的目录，默认为"/"
pullGet( = 自手机下载文件。\n成功返回 true ，失败返回 null, 错误信息。\n此函数不会调用 process.adb.logger 输出
pullGet(.("/mnt/sdcard/my->txt","本地路径",->->->) = 自手机下载文件。\n注意本地当前目录为 process.adb.workDir 指定的目录，默认为"/"
install( = 安装应用到手机。
install(.("apk文件路径",->->->) = 安装应用到手机。\n可用 process.adb.logger 预先设置进程回显对象
installUrl( = 自指定的网址下载 APK 并安装到手机。
installUrl(.("apk文件网址,->->->) = 自指定的网址下载 APK 并安装到手机。\n如果参数 @1 指定 HTML 页面，则自动自 HTML 页面提取 apk 下载链接。\n参数 @1 也可以指定本地 APK 文件路径。\n\n可用 process.adb.logger 预先设置进程回显对象。\n\n如果 APK 文件太大，建议改用 inet.downBox 或者 inet.httpFile 下载
uninstall(.("应用名") = 卸载应用。\n可用 process.adb.logger 预先设置进程回显对象
uninstall(.("应用名","-k") = 卸载应用,但保留配置和缓存
getSerialNo() = 返回当前连接的设备串号，\n出错返回 null，错误信息。\n\n如果当前连接了多个设备，且未用 setSerialNo 函数指定设备，\n此函数返回 null 值，错误信息
setSerialNo(__/*设备串号*/) = 指定设将命令发送至特定设备。\n参数 @1 指定设备序号，设为 null 则不指定特定设备。\n此函数返回参数 @1 。\n\n如果 process.adb.defaultArguments 添加了 {"-s","设备序号"} 以发送到特定设备，\n则覆盖 setSerialNo 函数指定的目标设备
isConnected() = 当前是否连接设备
getState() = 成功返回设备状态，"device" 为连接成功。\n出错返回 null，错误信息
getDevices() = 返回所有连接设备。\n如果参数 @1 为 true ，则允许返回离线设备。\n返回值为一个数组，数组元素为设备信息。\n设备信息的 serialNo 字段为设备串号，\nstate 字段为连接状态，"device" 为已连接，"offline" 表示离线。\ninfo 为设备信息，其中 product 为产品代号，model 为设备型号
waitForDevice() = 等待设备连接
startServer() = 启动 adb 服务端,成功返回 true ，\n\n此函数如果找到之前已启动的服务端会直接重用该服务并返回 true 。\n 失败返回 false, 错误信息 。\n\nadb 服务端为驻留进程用于接收命令并向手机转发指令\n而 adb 客户端进程在向服务端发送完指令后退出\n\n这个函数会在程序启动时自动调用一次
killServer() = 停止 adb 服务端,无返回值,\n adb 服务端为驻留进程用于接收命令并向手机转发指令,\n注意所有 adb 客户端共用也只能共用一个服务端
findServerPort() = 查找已运行的 adb 服务端端口
pair( = 无线配对手机设备\nAndroid 11 及更高版本支持
pair(.("IP:端口","配对码") = 无线配对手机设备\n参数 @1 示例 "192.168.1.103:35151"。\n请打开『手机开发者设置/无线调试/使用配对码配对设备』获取IP端口与配对码。\n注意配对端口与连接端口是不一样的。\n\n成功返回包含设备guid 的字符串，\n失败返回 null，错误信息
pairQr(.("IP:端口","配对码") = 扫描二维码后无线配对手机设备\n请打开『手机开发者设置/无线调试/使用二维码配对设备』扫描二维码。\n请调用 process.adb.qrCode 并传入相同参数生成二维码。\n注意配对端口与连接端口是不一样的。\n\n成功返回包含设备 guid 的字符串，\n失败返回 null，错误信息
connect( = 无线连接手机设备，\n需要先调用 pair 函数配对。
connect(.("IP:端口",设为默认设备) = 无线连接手机设备。\n可选参数 @2 指定连接成功是否自动调用 setSerialNo 函数设为默认设备，\n不指定参数 @2 则默认设为 true。\n\n请打开『手机开发者设置/无线调试』获取动态无线端口。\n或用 tcpip 函数设置固定端口并获取连接地址。\n如果 tcpip 函数保存了默认地址，那么 connect 函数可省略连接地址参数。\n\n连接成功返回连接地址，失败返回 null，错误信息。\n\n首次连接需要先调用 pair 函数配对设备。\n注意配对端口与连接端口不一样
disconnect() = 断开连接
tcpip(__) = 已连接手机后修改无线连接端口。\n参数@1 使用数值或字符串指定端口号，不指定则默认为 5555。\n成功返回"IP:端口"格式地址，失败返回 null,错误信息。\n如参数@2 为 true，则保存IP与端口以自动设置 connect 函数的默认参数。 \n\n如果参数 @1 为 false 则关闭此固定端口并切换到默认的 USB 模式。\n成功返回进程输出，失败返回 null，错误输出。\n\n此函数开启 TCPIP 固定端口以后，\n手机无线调试页面仍然会显示另一个动态端口号。\n并且每次调用这个函数，动态端口号都会变更
ip() = 返回手机 IP 地址
findDevice() = 查找已连接设备，并自动设置一个默认设备。\n如果当前仅连接一个设备，则不限定默认设备。\n如果参数 @1 为 true，则优先选择有线连接设备。\n如果参数为设备序号，优先选择指定设备。\n成功返回选择的默认设备序号
startCall(.(phone) = 拔打指定电话，需要在手机开发者选项中打开权限
startUrl(.(url) = 用浏览器打开参数 @url 指定的网址
userAgent = 用于下载 APK 的 Android 设备 User-Agent 
screenCap() = 手机截屏到指定文件。\n成功返回 true ，失败返回 null，错误信息
key(__) = 模拟手机按键。\n需要在手机开发者设置中打开模拟按键权限。\n可以使用其他 key 前缀的函数发送指定键码
getProp( = 获取属性。\n成功返回字符串类型的属性值，\n失败返回 null, 错误信息
getProp(.("ro->build->version->release") = @.getProp("ro.build.version.release")__/*返回 android 系统版本，\n成功返回一个字符串类型的数值*/
listPackages() = 列出所有应用。\n可选指定一个或多个字符串参数，\n"-s" 参数指定只返回系统应用，"-3"参数限定返回第三方应用。\n成功返回一个数组，不指定"-f","-i"参数时数组元素为包名。
getScreen() = 返回设备宽度与高度，有 2 个返回值。
getMaxPos() = 返回最大 x 坐标与最大 y 坐标。\n用于调用 getEvent 函数时转换输出的事件坐标值
getEvent() = 监听输入事件。\n可指定一个或多个参数，添加 "-l" 以文本代替数值显示事件类型与名称。\n返回值为 process.popen 对象。\n调用返回对象的 lines 函数可以遍历所有回显行并匹配需要的值。\n调用返回对象的 ctrlEvent(0) 函数可退出监听进程。 \n回显事件格式：设备 事件类型 事件名 事件值\n!process_popen.
input() = 发送输入事件，用法请 tap,swip 等函数源码与 adb 文档。
sendevent(__) = 发送输入事件，参数示例：\n"sendevent /dev/input/event3 3 57 0"
tap(.(x,y) = 发送触模事件，参数 x,y 指定坐标
swipe(.(x1,y1,x2,y2,duration) = 从 x1,y1 坐标滑动到 x2,y2 坐标。\n可选用 duration 指定持续时间，以毫秒为单位。\n原地滑动发送长按事件
sendString(.(text) = 发送输入文本事件，text 指定要输入的字符串
key(.(code,longPress) = 发送按键事件。\ncode 可以为数值或文本键名。\n用法请参考 adb 文件。\n或改用 key 前缀的其他函数。\n参数 @2 为 true 时表示长按
lastAddress() = 无参数返回当前系统账号最后连接的手机 IP 地址与端口。\n参数 @1 指定字符串则修改最后自动连接地址与端口，\n传入空字符串清除保存的连接地址。\n\n一般不要修改此地址，应由 process.adb 自动维护。
end intellisense**/