//入门
import console.int;  
import py3;
 
//导入 Python 模块
var math = py3.import("math");

/*
调用 Python 模块的函数。
Python 函数返回的所有值都是 pyObject 引用。
哪怕是一个整数值，也是  pyObject 对象。 
*/
var pyInt = math.floor(22.3);

//调用 pyObject 的成员函数获取类型名：
var typename = pyInt.type(); 

//转换为纯 aardio 数值，pyObject 支持 tonumber(),tostring() 等类型转换函数。
var n = tonumber( pyInt );  

//所有 pyObject 都可以调用 parseValue() 转换为 aardio 纯值类型。
var v = pyInt.parseValue();
/*
pyObject 也支持用 table.parseValue 转换为 aardio 纯值类型。
aardio 纯值类型指的是字符串、数值、布尔值、buffer、指针、纯值表。
纯值类型可以保持原值序列化，并与 JSON 兼容。
*/

//pyObject 自动支持 web.json
console.dumpJson(pyInt)