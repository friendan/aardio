//Ping 回调推送
import win.ui;
/*DSG{{*/
var winform = win.form(text="Ping";right=759;bottom=469)
winform.add()
/*}}*/

import web.view;
var wb = web.view(winform);

import process.popen;
wb.external = {
	ping = function(domain){
		
		//同步有同步的优势，扬长避短是智慧，倒行逆施最累人。 
		var prcs,err = process.popen("ping "+ domain); //请参考范例 > 进程 > 管道
		
		/*
		如果创建进程失败，err 为错误信息，
		aardio 中抛出异常，网页 DevTools 调试工具可查看错误信息
		*/
		//assert(prcs,err);
		
		for( all,out,err in prcs.each() ){
		    wb.invoke("document.body.insertAdjacentText",'beforeend',all); 
		}
		
		return "恭喜,事做好了!"
	} 
}

wb.html = /**
<body style="white-space: pre;"><script>
doSomething = async() => {
	
	//异步有异步的好处，扬长避短是智慧，倒行逆施最累人。
  	var result = await aardio.ping('www.example.com');
  	document.body.insertAdjacentText('beforeend',result);
};
</script>
<button  onclick="doSomething()">开始干活了</ping>
**/

winform.show();
win.loopMessage();