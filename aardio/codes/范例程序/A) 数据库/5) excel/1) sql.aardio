//SQL 操作
//下面的代码仅使用系统组件,不需要安装 Access / Excel

import access;

//可自动创建文件,系统自带 Jet 4.0 驱动可支持 Excel 8.0( 97-2003 )  
var db,err = access( "/test.xls") 

/* 
import access.oleDb12;
access.oleDb12.install();//操作 *.xlsx 需要检测并安装 Microsoft.Ace.OLEDB.12 驱动

var db,err = access( "/test.xlsx",//也可以省略后面的参数
	{  
        ["Provider"] = "Microsoft.Ace.OLEDB.12.0"; 
        ["Extended Properties"] = "Excel 12.0 Xml;HDR=YES"; 
    }  
)

关于列数据类型不统一时值丢失或字符串被截断,或使用 Excel 源的其他问题,可参考以下链接：
https://learn.microsoft.com/sql/integration-services/data-flow/excel-source
http://www.connectionstrings.com/excel/
*/

//创建表	
if( ! db.existsTable( "sheet1" ) ){
	db.exec("CREATE TABLE sheet1 (username VARCHAR(255),comment MEMO,num double )");
}
	
//插入数据,注意访问excel里的表名后面加$并用放到方括号里
db.exec( "INSERT INTO [sheet1$](username,comment,num)values('aardio','www.aardio.com',123)");

//查询
import console;
for(rs in db.each("SELECT * FROM [sheet1$]") ){  
	console.log( rs("username").value );
	console.log( rs("comment").value );
	console.log( rs("num").value )
}

//关闭数据库
db.close();
console.pause();