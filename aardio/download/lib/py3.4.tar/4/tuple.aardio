//tuple 元组
import util.metaProperty;
namespace py3;
	
class tuple {

	ctor( object ){ 
	
		var tt = type(object) 
		select(tt) {
			case type.table{ 
			
				if(object[["pyObject"]] ){
					
					
					if(object.checkTuple()){
						this[["pyObject"]] = object[["pyObject"]];
						..py3.IncRef(this.pyObject);	
					}
					else {
						var tuple =  ..py3.builtin.tuple(object);
						if(tuple.checkTuple()){
							this[["pyObject"]] = tuple.stealPtr()
						}
						else {
							return null;
						}
					}
				}
			  	else{
					var len = #object;
					this.pyObject = pyTuple_New(len); 
					
					for(i=1;len;1){
						if( type(object[i]) == "pointer" ){
							..py3.IncRef(object[i]);
							pyTuple_SetItem(this.pyObject,i-1,object[i] );  
						}
						else{
							pyTuple_SetItem(this.pyObject,i-1,..py3.object(object[i]).stealPtr() );  
						}
					};
				}
			}
			case type.pointer{
				this.pyObject = object;
			}
			case type.number{ 
				this.pyObject = pyTuple_New(object);
			} 
			case type.null{
				this.pyObject = pyTuple_New(0);
			}
			else { 	
				error("py3.tuple 构造参数不支持此数据类型:" + tt ,2) 
			} 
		}
		
		..table.add_gc(this,"release");
	}
	@_metaProperty
}

namespace tuple {

	_metaProperty = ..util.metaProperty(   
	
		_topointer = function(){
			if(!owner.pyObject) error("Python Tuple已销毁",2);
			return owner.pyObject;
		}  
		parseValue = function(){
			var result = {}; 
			var len = pyTuple_Size(owner.pyObject);
			for(i=0;len-1;1){
				var item = pyTuple_GetItem(owner.pyObject,i);
				if(item){
					item = ..py3.object(  item,true );
					result[i+1] = item.parseValue();
					item.stealPtr();//因为item是借用，所以盗用还回去
				}
			}
			return result; 
		}
		getItem = function(k){
			return owner[k]
		}; 
		setItem = function(k,v){
			owner[k] = v;
		};  
		__get = function(k,ownerCall){ 
			if( type(k) != type.number ) {
				return owner.getAttr(k); 
			}
			
			var item = pyTuple_GetItem(owner.pyObject,k);
			if(item){
				..py3.IncRef(item);//特例 borrow reference
				return ..py3.object(  item,true ) ;
			} 
		} 
		__set = function(k,v){
			if( type(k) != type.number ) {
				return;
			}
			
			if(v!==null) {
				if( type(v) == "pointer" ){
					..py3.IncRef(v);
					pyTuple_SetItem(this.pyObject,k,v );  
				}
				else{
					pyTuple_SetItem(owner.pyObject,k,..py3.object(v).stealPtr() ); 
				} 
			} 
		}
		len = function(){
			return pyTuple_Size(owner.pyObject);
		} 
		_type = "py3.tuple";
	);
	..table.mix(_metaProperty,..py3.object._metaProperty); 
	
	pyTuple_New = ::Python3.api("PyTuple_New","pyObject(INT len)")  
	pyTuple_GetItem = ::Python3.api("PyTuple_GetItem","pyObject(PyObject dp,INT key)")  
	pyTuple_SetItem = ::Python3.api("PyTuple_SetItem","pyObject(PyObject dp,INT key,pyObject item)") 
	pyTuple_Size = ::Python3.api("PyTuple_Size","INT(PyObject dp)") 
	
	buildValueByNumber =  ..py3._buildValueByNumber;
	buildValueByString =  ..py3._buildValueByString;
}


/**intellisense()
py3.tuple( = 元数组是只读的序列数组,数组元素不可变更\n此对象继承自pyObject(py3.object)\n一般不需要手动创建此对象,aardio会在传参数时自动转换
py3.tuple(.(长度) = 创建新的元数组
py3.tuple(.(pyObject指针) = 创建新的元数组,\n不会添加引用计数,但对象销毁时负责释放引用计数
py3.tuple(.(pyObject对象) = 如果传入 Python 对象是一个 tuple,\n返回绑定相同对象的 py3.tuple 对象,添加引用计数,对象销毁时负责释放引用计数\n否则将传入对象作为 python 内置函数 tuple 的参数并返回 py3.tuple 对象\n失败返回 null
py3.tuple(.(aardio数组) = 创建新的元数组,\n如果数组成员中包含pointer类型指针,必须是pyObject指针,保持引用计数不变
!pyTuple.stealPtr() =  接管此对象的指针并盗用一次引用计数\n原对象的内部指针被清空,并不再负责释放引用计数\n steal references
!pyTuple.parseValue() = 转换为aardio数组
!pyTuple.type() = 返回类型名字
!pyTuple.has("__/*属性名*/") = 是否存在指定的属性
!pyTuple.getAttr("__/*属性名*/") = 读属性成员,也可以用成员操作符获取\n属性值为 py3.object 对象
!pyTuple.setAttr("__/*属性名*/",) = 写属性成员的值,也可以用成员操作符赋值
!pyTuple.getAttr() = !p3yObject.
!pyTuple.getItem(__/*索引*/) = 返回指定项,类似在python中使用下标操作符取值
!pyTuple.setItem(__/*索引*/，) = 修改指定项,类似在python中使用下标操作符赋值
!pyTuple.getItem() = !p3yObject.
!pyTuple.toString() = 转换为 aardio 字符串\n也可以直接将对象作为参数传入 tostring 函数转换为字符串
!pyTuple.toTuple() = 转换为 Python 元组\n!pyTuple.
!pyTuple.toList() = 转换为 Python 列表\n!pyList.
!pyTuple.checkList() = 检测是否列表
!pyTuple.each() = @for( item in ??.each() ){\n	__/*创建迭代器用于遍历所有的项,返回一个值*/\n}\n!p3yObject.
!pyTuple.? = !pyObject.
py3.tuple() = !pyTuple.
end intellisense**/