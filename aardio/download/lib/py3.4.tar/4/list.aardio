//list 列表数组
import util.metaProperty;
namespace py3;
	
class list {

	ctor( object ){ 

		var tt = type(object) 
		select(tt) {
			case type.pointer{
				this.pyObject = object;
			}
			case type.null{
				this.pyObject = PyList_New(0);
			}
			case type.number{
				this.pyObject = PyList_New(object);
			}
			case type.table{ 
			
				if(object[["pyObject"]] ){
					if( object.checkList() ){
						this[["pyObject"]] = object[["pyObject"]];
						..py3.IncRef(this.pyObject);
					}
					else {
						var list =  ..py3.builtin.list(object);
						if(list.checkList()){
							this[["pyObject"]] = list.stealPtr()
						}
						else {
							return null;
						}
					}
					
				}
			  	else{
					var len = #object;
					this.pyObject = PyList_New(len); 
					
					for(i=1;len;1)
						PyList_SetItem(this.pyObject,i-1,..py3.object(object[i]).stealPtr() );   
				}
			}
			else {
				error("pyObject构造参数不支持此数据类型:" + tt ,2);
			} 
		}
		
		..table.add_gc(this,"release");
	}
	@_metaProperty
}

namespace list {

	_metaProperty = ..util.metaProperty(   {
	
		_topointer = function(){
			if(!owner.pyObject) error("Python List已销毁",2);
			return owner.pyObject;
		} 
		
		parseValue = function(baseType){
			var result = {}; 
			var len = PyList_Size(owner.pyObject);
			for(i=0;len-1;1){
				var item = PyList_GetItem(owner.pyObject,i);
				if(item){
					item = ..py3.object(  item,true );
					result[i+1] = item.parseValue(baseType);
					item.stealPtr();//因为item是借用，所以盗用还回去
				}
			}
			return result; 
		}
		  
		getItem = function(k){
			return owner[k]
		}  
		
		setItem = function(k,v){
			owner[k] = v;
		}
		
		append = function(v){
			if(v!==nul)PyList_Append(owner.pyObject,..py3.object(v) ); 
		}	
		
		__get = function(k,ownerCall){ 
			var t = type(k);
			if( t==="string" && (ownerCall!==null) ){
				return owner.getAttr(k); 
			} 
			
			if( t == "number" ) {
				if(k<0){
					k = owner.len()-1;
				}
				
				var item = PyList_GetItem(owner.pyObject,k);
				if(item){
					..py3.IncRef(item);//特例 borrow reference
					return ..py3.object(item,true).parseValue(true);
				} 
			} 
		}
		__set = function(k,v,ownerAttr){
			var t = type(k);
			if( t==="string" && ownerAttr ){
				owner.setAttr(k,v);
				return;
			}
			
			if( t != "number" ) return ;
			
			if(v===null)
				PyList_DelItem(owner.pyObject,k);
			else {
				if( type(v) == "pointer" ){
					..py2.IncRef(v);
					PyList_SetItem(this.pyObject,k,v );  
				}
				else{
					PyList_SetItem(owner.pyObject,k,..py3.object(v).stealPtr()  ); 
				}
			} 
		}
		len = function(){
			return PyList_Size(owner.pyObject);
		}
		_type = "py3.list";
	} );
	..table.mix(_metaProperty,..py3.object._metaProperty); 
	
	PyList_New = ::Python3.api("PyList_New","pyObject(int size)")  
	PyList_GetItem = ::Python3.api("PyList_GetItem","pyObject(PyObject dp,INT key)")  
	PyList_SetItem = ::Python3.api("PyList_SetItem","pyObject(PyObject dp,INT key,pyObject item)") 
	PyList_Append = ::Python3.api("PyList_Append","pyObject(PyObject dp,pyObject item)")  
	PyList_Size = ::Python3.api("PyList_Size","INT(PyObject dp)") 
}


/**intellisense()
py3.list( = 创建新的列表。\n此对象继承自pyObject（py3.object）。\n一般不需要手动创建此对象，aardio 会在传参数时自动转换
py3.list(.(列表长度) = 创建新的列表。\n也可以省略参数
py3.list(.(pyObject指针) = 创建新的python列表,\n不会添加引用计数,但对象销毁时负责释放引用计数
py3.list(.(pyObject对象) = 如果传入 Python 对象是一个 list,\n返回绑定相同对象的 py3.list 对象,添加引用计数,对象销毁时负责释放引用计数\n否则将传入对象作为 python 内置函数 list 的参数并返回 py3.list 对象\n失败返回 null
py3.list(.(aardio数组) = 参数传入aardio数组,\n创建并返回新的的python列表\n注意 python中下标自0开始,而aardio中下标是自1开始
!pyList.stealPtr() =  接管此对象的指针并盗用一次引用计数\n原对象的内部指针被清空,并不再负责释放引用计数\n steal references
!pyList.parseValue() = 转换为 aardio 数组。\n除浮点数、53存储位以下整数、布尔值、字节数组之外的类型通过 JSON 转换为 aardio 值。
!pyList.parseValue() = 转换为 aardio 数组。\n保留除浮点数、53存储位以下整数、布尔值、字节数组之外的类型为 PyObject 不作转换。
!pyList.type() = 返回类型名字
!pyList.has("__/*属性名*/") = 是否存在指定的属性
!pyList.getAttr("__/*属性名*/") = 读属性值，也可以用成员操作符获取。\n除数值、布尔值、字符串、字节数组以外的值在 aardio 中存为 py.object 对象
!pyList.setAttr("__/*属性名*/",) = 写属性成员的值，也可以用成员操作符赋值。
!pyList.getAttr() = !p3yObject.
!pyList.getItem(__/*索引*/) = 返回指定列表项，也可以用索引下标操作符 [] 取值。\nPython 列表起始下标为 0，可用负数索引表示从尾部计数。\n注意：普通 aardio 数组起始下标为 1，不能使用负数计数
!pyList.setItem(__/*索引*/，) = 修改指定列表项，也可以用索引下标操作符 [] 赋值。\nPython 列表起始下标为 0，可用负数索引表示从尾部计数。\n注意：普通 aardio 数组起始下标为 1，不能使用负数计数
!pyList.getAttr() = !pyObject.
!pyList.toString() = 转换为 aardio 字符串\n也可以直接将对象作为参数传入 tostring 函数转换为字符串
!pyList.toTuple() = 转换为 Python 元组\n!pyTuple.
!pyList.toList() = 转换为 Python 列表\n!pyList.
!pyList.checkList() = 检测是否列表
!pyList.each() = @for( item in ??.each() ){\n	__/*创建迭代器用于遍历所有的项,返回一个值*/\n}\n!pyObject. 
!pyList.? = !pyObject.
py3.list() = !pyList. 
end intellisense**/